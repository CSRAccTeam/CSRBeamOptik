!-------------------------------------------------
!------------ TRANSFER LINE CONSTANTS ------------
!-------------------------------------------------

const axgeo_BM1 = 45*PI/180;
const bendR_BM1 = 1.3;
const L_BM1 = bendR_BM1*axgeo_BM1;
ax_BM1 := axgeo_BM1 + dax_BM1;

const L_QD11 = 0.18;
const L_QD12 = 0.18;

const axgeo_BM2 = 45*PI/180;
const bendR_BM2 = 1.3;
const L_BM2 = bendR_BM2*axgeo_BM2;
ax_BM2 := axgeo_BM2 + dax_BM2;

const L_QD21 = 0.18;
const L_QD22 = 0.18;

const L_QD3  = 0.1594;
const L_QD4  = 0.1594;

!-------------------------------------------------
!------------- ELEMENT STRENGTHS -----------------
!-------------------------------------------------

dax_BM1 = 0.0;
dax_BM2 = 0.0;
kL_QD11 = -10.4885;
kL_QD12 = 11.4488;
kL_QD21 = 10.07;
kL_QD22 = -10.125;
kL_QD31 = 8.27;
kL_QD32 = -22.32;
kL_QD33 = 14.606;
kL_QD41 = -10.45;
kL_QD42 = 8.83;
kL_QD43 = 2.08;

!-------------------------------------------------
!------ ION SOURCE 300KV TRANSFER LINE -----------
!-------------------------------------------------

DRIFT1:       DRIFT, L=1;
BM1:          SBEND, L=L_BM1, ANGLE=axgeo_BM1,
	      	     !K0:= ax_BM1,
	             E1=0., E2=0.,
		     HGAP=0.025, FINT=0.7;
DRIFT2:       DRIFT, L=0.18;

DRIFT3:       DRIFT, L=0.12;
QD11:         QUADRUPOLE, L=L_QD11, K1:=kL_QD11;
DRIFT4:       DRIFT, L=0.2;
QD12:         QUADRUPOLE, L=L_QD11, K1:=kL_QD12;
DRIFT5:       DRIFT, L=1.2;
MONITOR1:     MONITOR;

DRIFT6:       DRIFT, L=0.18;
BM2:          SBEND, L=L_BM2, ANGLE=axgeo_BM2,
	      	     !K0:= axgeoBM2 + dax_BM2,
	             E1=0, E2=0,
		     HGAP=0.025, FINT=0.7;
DRIFT7:       DRIFT, L=0.15;

DRIFT8:       DRIFT, L=0.15;
QD21:         QUADRUPOLE, L=L_QD21, K1:=kL_QD21;
DRIFT9:       DRIFT, L=0.2;
QD22:         QUADRUPOLE, L=L_QD22, K1:=kL_QD22;
DRIFT10:      DRIFT, L=0.6;

WAND_ANFANG:  MONITOR;
DRIFT11:      DRIFT, L=0.5;
WAND_ENDE:    MONITOR;
! GAP + STEERER + GAP
DRIFT12:      DRIFT, L=0.64;           
MONITOR2:     MONITOR;
DRIFT13:      DRIFT, L=0.56;
MONITOR3:     MONITOR;

DEFLEKTOR:    DRIFT, L=0.47;
MONITOR4:     MONITOR;

DRIFT14:      DRIFT, L=1.175;
QD31:         QUADRUPOLE, L=L_QD3, K1:=kL_QD31; 
DRIFT15:      DRIFT, L=0.021;
QD32:         QUADRUPOLE, L=L_QD3, K1:=kL_QD32; 
DRIFT16:      DRIFT, L=0.021;
QD33:         QUADRUPOLE, L=L_QD3, K1:=kL_QD33;  
DRIFT17:      DRIFT, L=0.095;

PHOTO_D:      DRIFT, L=3; !Photodetachment 
BEAM_C:       DRIFT, L=1; !Beamcleaner  
DRIFT18:      DRIFT, L=0.15;
MONITOR5:     MONITOR;

DRIFT19:      DRIFT, L=0.325; 
QD41:         QUADRUPOLE, L=L_QD4, K1:=kL_QD41; 
DRIFT20:      DRIFT, L=0.021;
QD42:         QUADRUPOLE, L=L_QD4, K1:=kL_QD42; 
DRIFT21:      DRIFT, L=0.021;
QD43:         QUADRUPOLE, L=L_QD4, K1:=kL_QD43;

DRIFT22:      DRIFT, L=0.665;
! Außenseite des großen rechteckigen Außenflansches an der Injektionsecke
CSRDECKEL:    MONITOR; 
DRIFT23:      DRIFT, L=0.026;
DRIFT24:      DRIFT, L=3.3725;

!--------------------------------------------------
!-------------- 1 SECTOR VON CSR ------------------
!--------------------------------------------------

CSR_QD11:        QUADRUPOLE, L=0.2, k1=5.511707;
CSR_DRIFT1:      DRIFT, L=0.15;
CSR_QD12:        QUADRUPOLE, L=0.2, k1=-6.90466;
CSR_DRIFT2_sec:  DRIFT, L=1.4/20;
CSR_DRIFT2:      LINE=20*(CSR_DRIFT2_sec); 
CSR_MITTE:       MONITOR;

!--------------------------------------------------
!----------------- TRANSFER LINE ------------------
!--------------------------------------------------

IQ300: LINE=(DRIFT1,  BM1,  DRIFT2,
             DRIFT3,  QD11, DRIFT4, QD12, DRIFT5,  MONITOR1,
	     DRIFT6,  BM2,  DRIFT7,
	     DRIFT8,  QD21, DRIFT9, QD22, DRIFT10, WAND_ANFANG,
	     DRIFT11, WAND_ENDE, DRIFT12, MONITOR2,
	     DRIFT13, MONITOR3, DEFLEKTOR, MONITOR4,
	     DRIFT14, QD31, DRIFT15, QD32, DRIFT16, QD33,
	     DRIFT17, PHOTO_D, BEAM_C, DRIFT18, MONITOR5,
	     DRIFT19, QD41, DRIFT20, QD42, DRIFT21, QD43,
	     DRIFT22, CSRDECKEL);

!--------------------------------------------------
!---------------------- END -----------------------
!--------------------------------------------------