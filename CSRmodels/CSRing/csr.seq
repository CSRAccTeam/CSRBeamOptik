! Original MAD8 File from M. G. Januar 2011
! Editing: C. Cortes 06.2019
! In this file, the components of the CSR are defined
! and set for MADX
!
!----------------------------------------------------
!------------------ GLOBALS -------------------------
!----------------------------------------------------
! E=20 keV Proton

TITLE, "CRIOGENIC STORAGE RING";

ENERGY:= 0.02/1000; ! Energie in GeV
MASSE0:= 0.9385;     ! Masse;

EGES  := MASSE0+ENERGY;
GAMMA := EGES/MASSE0;
BETA  := SQRT(1-1/(GAMMA*GAMMA));

VALUE, GAMMA;
VALUE, BETA;

emmix := 100e-6;    ! definiert 체ber 2-sigma
emmiy := 100e-6;    ! definiert 체ber 2-sigma
DELPP := 0.01*BETA; ! 2 sigma Wert von Deltap_p

Ekin_classisch := 0.5*masse0*1e6*beta*beta;
VALUE, Ekin_classisch;

!-----------------------------------------------
!------------- Cylinder Deflector 39째 ----------
!-----------------------------------------------
! LD - Path length
! rho-radius
! BETA - velocity
! gamma-E/mc**2

RHO:=1.;
LD:=(RHO*pi/180)*39./10;
RK:=SQRT(2.)/RHO;
DISP:=2./RHO;

DEFLECTOR_SEC :  MATRIX, L=LD,
	      	 RM11=COS(RK*LD),
		 RM12=(SIN(RK*LD))/RK, 
             	 RM16=(1-COS(RK*LD))*DISP/RK/RK/BETA, 
                 RM21=-SIN(RK*LD)*RK,
		 RM22=COS(RK*LD), 
                 RM26=SIN(RK*LD)*DISP/RK/BETA, 
                 RM33=1.,
		 RM34=LD, 
                 RM43=0.,
		 RM44=1., 
                 RM51=-SIN(RK*LD)*DISP/RK/BETA, 
                 RM52=-(1-COS(RK*LD))*DISP/RK/RK/BETA, 
                 RM55=1.0, 
                 RM56=(-(LD-SIN(RK*LD)/RK)*DISP*DISP/RK/RK+LD/GAMMA/GAMMA)/BETA/BETA, 
                 RM66=1.0;
VKICK:           vkicker;
  
!h1: hkicker,kick=0.1*3.14/180;
!Deflektor mit Feld Fehler
!Deflektor_f: line=(a,(DEFLECTOR_SEC,a),h1,(DEFLECTOR_SEC,a));

!--------------------------------------------------
!------------- Cylinder Deflector 6째 --------------
!--------------------------------------------------
! LD_K - Path length
! RHO_K-radius
! BETA - velocity
! gamma-E/mc**2

RHO_K:=2;
LD_K:=(RHO_K*pi/180)*6./5;

RK_K:=SQRT(2.)/RHO_K;
DISP_K:=(2.)/RHO_K;

DEFLECTOR_K_SEC :  MATRIX, L=LD_K,
		   RM11=COS(RK_K*LD_K),
  		   RM12=(SIN(RK_K*LD_K))/RK_K,
  		   RM16=(1-COS(RK_K*LD_K))*DISP_K/RK_K/RK_K/BETA, 
  		   RM21=-SIN(RK_K*LD_K)*RK_K,
		   RM22=COS(RK_K*LD_K), 
  		   RM26=SIN(RK_K*LD_K)*DISP_K/RK_K/BETA,
  		   RM33=1,
		   RM34=LD_K, 
  		   RM43=0,
		   RM44=1,
  		   RM51=-SIN(RK_K*LD_K)*DISP_K/RK_K/BETA, 
  		   RM52=-(1-COS(RK_K*LD_K))*DISP_K/RK_K/RK_K/BETA,
		   RM55=1.0,
  		   RM56=(-(LD_K-SIN(RK_K*LD_K)/RK_K)*DISP_K*DISP_K/RK_K/RK_K+
			LD_K/GAMMA/GAMMA)/BETA/BETA, 
  		   RM66=1.0;			     

!-----------------------------------------------------------
!--------------------- BEAM LINE --------------------------- 
!-----------------------------------------------------------

! skalierung=1.059599;  ! Skalierfaktor von Tosca
! One sector of the machine

DRIFT1:           Drift, L=1.4;
QD11:             Quadrupole, L=0.2, k1=5.511707;
DRIFT2:           Drift, L=0.15;
QD12:             Quadrupole, L=0.2, k1=-6.90466;
DRIFT3:           Drift, L=0.2;

DEFLECTOR6_1:     line=(VKICK, 5*(DEFLECTOR_K_SEC,VKICK));
DRIFT4:           Drift, L=1.1;
DEFLECTOR39_1:    line=(VKICK, 10*(DEFLECTOR_SEC,VKICK));
DRIFT5:           Drift, l=0.5;
DEFLECTOR39_2:    line=(VKICK, 10*(DEFLECTOR_SEC,VKICK));
DRIFT6:           Drift, L=1.1;
DEFLECTOR6_2:     line=(VKICK, 5*(DEFLECTOR_K_SEC,VKICK));

DRIFT7:           Drift, L=0.2;
QD21:             Quadrupole, L=0.2, k1=-6.90466;
DRIFT8:           Drift, L=0.15;
QD22:             Quadrupole, L=0.2, k1=5.511707;
DRIFT9:           Drift, L=1.4;

Section1:  line=(DRIFT1, QD11, DRIFT2, QD12, DRIFT3,
	         DEFLECTOR6_1, DRIFT4, DEFLECTOR39_1,
                 DRIFT5, DEFLECTOR39_2, DRIFT6, DEFLECTOR6_2,
		 DRIFT7, QD21, DRIFT8, QD22, DRIFT9);

csring: line=(Section1 , -Section1, Section1, -Section1);

!----------------------------------------------------------
!------------ END BEAMLINE DEFINITION ---------------------
!----------------------------------------------------------